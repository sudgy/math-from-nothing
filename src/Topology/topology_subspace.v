Require Import init.

Require Export topology_base.
Require Export topology_axioms.
Require Export topology_basis.

(* begin hide *)
Section SubspaceTopology.

Local Open Scope set_scope.

Context {U} `{Topology U}.
(* end hide *)
Variable X : U â†’ Prop.

Program Instance subspace_topology : Topology (set_type X) := {
    open S := âˆƒ T, open T âˆ§ S = to_set_type X T
}.
Next Obligation.
    rename H0 into sub.
    unfold subset in sub.
    pose (S' A := âˆƒ s, A = ex_val (sub [s|] [|s])).
    exists (â‹ƒ S').
    split.
    -   apply union_open.
        intros A [s A_eq].
        rewrite A_eq; clear A A_eq.
        rewrite_ex_val A A_H.
        apply A_H.
    -   apply predicate_ext.
        intros x.
        split.
        +   intros [A [SA Ax]].
            exists (ex_val (sub A SA)).
            split.
            --  exists [A|SA].
                cbn.
                reflexivity.
            --  rewrite_ex_val B B_H.
                destruct B_H as [B_open A_eq].
                subst A.
                apply Ax.
        +   intros [A [[s A_eq] Ax]].
            subst A.
            rewrite_ex_val t tH.
            destruct tH as [t_open s_eq].
            exists [s|].
            split.
            *   exact [|s].
            *   rewrite s_eq.
                assumption.
Qed.
Next Obligation.
    rename H0 into sub, H1 into S_fin.
    unfold subset in sub.
    pose (S' A := âˆƒ s, A = ex_val (sub [s|] [|s])).
    exists (â‹‚ S').
    split.
    -   apply inter_open.
        +   intros A [s A_eq].
            rewrite A_eq; clear A A_eq.
            rewrite_ex_val A A_H.
            apply A_H.
        +   apply (le_lt_trans2 S_fin).
            clear S_fin.
            unfold le; equiv_simpl.
            exists (Î» (A : set_type S'), ex_val [|A]).
            split.
            intros [A [[A' SA'] A_eq]] [B [[B' SB'] B_eq]] eq; cbn in *.
            apply set_type_eq; cbn.
            subst A B.
            cbn in *.
            revert eq.
            rewrite_ex_val x x_eq.
            rewrite_ex_val y y_eq.
            intros eq.
            rewrite x_eq, y_eq.
            subst x.
            reflexivity.
    -   apply predicate_ext.
        intros x.
        split.
        +   intros all_A.
            intros A [s A_eq].
            rewrite A_eq; clear A A_eq.
            rewrite_ex_val A A_H.
            destruct A_H as [A_open s_eq].
            specialize (all_A [s|] [|s]).
            rewrite s_eq in all_A.
            apply all_A.
        +   intros all_A A SA.
            assert (S' (ex_val (sub A SA))) as S'A.
            {
                exists [A|SA].
                reflexivity.
            }
            specialize (all_A (ex_val (sub A SA)) S'A).
            rewrite_ex_val A' A'H.
            destruct A'H as [A'_open A_eq].
            rewrite A_eq.
            exact all_A.
Qed.

Theorem subspace_open : âˆ€ S : U â†’ Prop, open S â†’ open (to_set_type X S).
Proof.
    intros S S_open.
    exists S.
    split.
    -   exact S_open.
    -   reflexivity.
Qed.

Theorem open_subspace_open : open X â†’ âˆ€ S : U â†’ Prop,
    S âŠ† X â†’ open (to_set_type X S) â†’ open S.
Proof.
    intros X_open S sub S_open.
    unfold open in S_open; cbn in S_open.
    destruct S_open as [T [T_open T_eq]].
    assert (S = T âˆ© X) as eq.
    {
        apply predicate_ext.
        intros x; split.
        -   intros Sx.
            specialize (sub x Sx).
            assert (to_set_type X S [x|sub]) as x_in by exact Sx.
            rewrite T_eq in x_in.
            split.
            +   exact x_in.
            +   exact sub.
        -   intros [Tx Xx].
            assert (to_set_type X T [x|Xx]) as x_in by exact Tx.
            rewrite <- T_eq in x_in.
            exact x_in.
    }
    rewrite eq.
    apply inter_open2; assumption.
Qed.

Theorem subspace_inter_closed : âˆ€ A B, closed B â†’ A = B âˆ© X â†’
    closed (to_set_type X A).
Proof.
    intros A B B_closed A_eq.
    unfold closed in B_closed.
    assert (open (Î» x : set_type X, (ð‚ B) [x|])) as B'_open.
    {
        unfold open; cbn.
        exists (ð‚ B).
        split.
        -   exact B_closed.
        -   apply antisym; cbn.
            +   intros [x Xx] nBx; cbn in *.
                exact nBx.
            +   intros [x Xx] nBx; cbn in *.
                exact nBx.
    }
    unfold closed.
    assert (ð‚ (to_set_type X A) =
        (Î» x : set_type X, ð‚ B [x|])) as eq.
    {
        apply antisym.
        -   intros [x Xx] nAx Bx; cbn in *.
            apply nAx.
            rewrite A_eq.
            split; assumption.
        -   intros [x Xx] nBx Ax; cbn in *.
            rewrite A_eq in Ax.
            destruct Ax; contradiction.
    }
    rewrite eq.
    exact B'_open.
Qed.
Theorem subspace_closed_inter : âˆ€ A, A âŠ† X â†’ closed (to_set_type X A) â†’
    âˆƒ B, closed B âˆ§ A = B âˆ© X.
Proof.
    intros A sub A'_closed.
    unfold closed in A'_closed.
    destruct A'_closed as [S [S_open S_eq]].
    exists (ð‚ S).
    split.
    +   apply open_complement_closed.
        exact S_open.
    +   apply (f_equal ð‚) in S_eq.
        rewrite compl_compl in S_eq.
        unfold ð‚ in S_eq.
        apply antisym.
        *   intros x Ax.
            assert (to_set_type X A [x|sub x Ax]) as x_in.
            {
                exact Ax.
            }
            rewrite S_eq in x_in.
            split.
            ++  exact x_in.
            ++  exact (sub x Ax).
        *   intros x [nSx Xx].
            assert ((Î» x : set_type X, Â¬to_set_type X S x) [x|Xx])
                as x_in by exact nSx.
            rewrite <- S_eq in x_in.
            exact x_in.
Qed.

Theorem closed_subspace_closed : closed X â†’ âˆ€ S : U â†’ Prop,
    S âŠ† X â†’ closed (to_set_type X S) â†’ closed S.
Proof.
    intros X_closed S sub S_closed.
    unfold closed, open in S_closed; cbn in S_closed.
    destruct S_closed as [T [T_open T_eq]].
    unfold closed.
    assert (ð‚ S = T âˆª (ð‚ X)) as eq.
    {
        apply antisym.
        -   intros x nSx.
            classic_case (X x) as [Xx|nXx].
            +   assert ((ð‚ (to_set_type X S)) [x|Xx]) as x_in
                    by exact nSx.
                rewrite T_eq in x_in.
                left; apply x_in.
            +   right; exact nXx.
        -   intros x x_in.
            classic_case (X x) as [Xx|nXx].
            +   destruct x_in as [Tx|nXx]; try contradiction.
                assert (to_set_type X T [x|Xx]) as x_in by exact Tx.
                rewrite <- T_eq in x_in.
                exact x_in.
            +   intros Sx.
                apply sub in Sx.
                contradiction.
    }
    rewrite eq.
    apply union_open2; assumption.
Qed.

(* begin hide *)
End SubspaceTopology.

Section SubspaceBasis.

Context {U} `{TopologyBasis U}.
Existing Instance subspace_topology.
Variable X : U â†’ Prop.
(* end hide *)
Let C S := âˆƒ B, top_basis B âˆ§ S = to_set_type X B.

Lemma subspace_basis_open : C âŠ† open.
Proof.
    intros S [B [B_basis eq]].
    rewrite eq.
    exists B.
    split; try reflexivity.
    apply basis_open.
    exact B_basis.
Qed.

Lemma subspace_basis_contains : âˆ€ S x, open S â†’ S x â†’ âˆƒ B, C B âˆ§ B âŠ† S âˆ§ B x.
Proof.
    intros S x [T [T_open S_eq]] Sx.
    subst S.
    specialize (T_open [x|] Sx) as [B [B_basis [Bx B_sub]]].
    exists (to_set_type X B).
    repeat split.
    -   exists B.
        split; trivial.
    -   apply to_set_type_sub.
        exact B_sub.
    -   exact Bx.
Qed.

Definition subspace_basis_topology :=
    (make_basis_topology C subspace_basis_open subspace_basis_contains).
Existing Instance subspace_basis_topology.

Theorem subspace_basis_eq : basis_topology = (subspace_topology X).
Proof.
    apply make_basis_equal.
Qed.

(* begin hide *)
End SubspaceBasis.
(* end hide *)
Section SubspaceHausdorff.

(* begin hide *)
Local Open Scope set_scope.
(* end hide *)
Context {U} `{HausdorffSpace U}.
Variable X : U â†’ Prop.
Existing Instance subspace_topology.

Program Instance subspace_hausdorff : HausdorffSpace (set_type X).
Next Obligation.
    rename H1 into neq'.
    destruct x1 as [x1 Xx1], x2 as [x2 Xx2]; cbn.
    assert (x1 â‰  x2) as neq.
    {
        intro; subst.
        apply neq'.
        apply set_type_eq; reflexivity.
    }
    clear neq'.
    pose proof (hausdorff_space x1 x2 neq)
        as [S1 [S2 [S1_open [S2_open [S1x [S2x dis]]]]]].
    exists (Î» x, S1 [x|]), (Î» x, S2 [x|]).
    repeat split.
    -   exists S1.
        split.
        +   exact S1_open.
        +   apply predicate_ext.
            intros x.
            reflexivity.
    -   exists S2.
        split.
        +   exact S2_open.
        +   apply predicate_ext.
            intros x.
            reflexivity.
    -   exact S1x.
    -   exact S2x.
    -   apply empty_eq.
        intros [y y_in] [S1y S2y]; cbn in *.
        apply (land (empty_eq _) dis y).
        split; assumption.
Qed.

End SubspaceHausdorff.
